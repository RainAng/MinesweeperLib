group 'com.github.rainang'
version '0.8.0'

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
	manifest {
		attributes "Implementation-Title": 'MinesweeperLib',
				"Implementation-Version": version,
				"Main-Class": 'com.github.rainang.minesweeperlib.ConsoleDemo'
	}
}

task filterSrc(type: Copy) {
	from(sourceSets.main.allJava) {
		include '**/Minesweeper.java'
		expand version: version
	}
	from(sourceSets.main.allJava) {
		exclude '**/Minesweeper.java'
	}
	from sourceSets.main.resources
	into getTemporaryDir()
}

compileJava {
	dependsOn filterSrc
	source = filterSrc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}